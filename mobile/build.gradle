apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("config.properties")
if (localPropertiesFile.isFile()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
} else {
    logger.log(LogLevel.ERROR, "Can't find file <config.properties>. Please see <config-tmpl.properties>.")
    localProperties['ks.file'] = "n/a"
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "cc.chenhe.weargallery"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 220600010 // header(22)+xx.xx.xx+device(0-phone; 1-wear)
        versionName "v6.0.1-preview"

        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile file(localProperties['ks.file'])
            storePassword localProperties['ks.pwd']
            keyAlias localProperties['ks.alias']
            keyPassword localProperties['ks.aliaspwd']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = 1.8
    }
}

dependencies {
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    wearApp project(':wear')
    implementation project(':common')

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "androidx.viewpager2:viewpager2:$viewpager2Version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation("androidx.lifecycle:lifecycle-service:$lifecycleVersion")
    implementation("androidx.work:work-runtime-ktx:2.5.0")
    implementation "io.insert-koin:koin-android:$koinVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"
    implementation "androidx.core:core-ktx:$ktxVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesAndroidVersion"

    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation 'com.google.android.gms:play-services-wearable:17.1.0'
    implementation "id.zelory:compressor:3.0.1"
    implementation "com.heinrichreimersoftware:material-intro:$materialIntroVersion"
    implementation("me.chenhe:wearmsger:$wearMsgerVersion")


    // AndroidLinks
    implementation 'com.drakeet.about:about:2.4.1'
    implementation 'com.drakeet.multitype:multitype:4.0.0'
    implementation 'com.drakeet.about:about-extension:2.4.1'
    //noinspection GradleDependency
    implementation 'com.alibaba:fastjson:1.1.70.android' // required by android links

    // pictures
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation "com.davemorrissey.labs:subsampling-scale-image-view:$subsamplingImageViewVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "androidx.test:core:$testVersion" // Robolectric environment
    testImplementation "org.mockito:mockito-core:$mockitoVersion" // Mockito framework
    testImplementation "org.amshove.kluent:kluent-android:$kluentVersion"
}
