apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.ticwear.application'
//firebase
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("config.properties")
if (localPropertiesFile.isFile()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
} else {
    logger.log(LogLevel.ERROR, "Can't find file <config.properties>. Please see <config-tmpl.properties>.")
    localProperties['ks.file'] = "n/a"
}

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "cc.chenhe.weargallery"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 220060000
        versionName "v6.0.0-preview"

        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile file(localProperties['ks.file'])
            storePassword localProperties['ks.pwd']
            keyAlias localProperties['ks.alias']
            keyPassword localProperties['ks.aliaspwd']
        }
    }
    buildTypes {
        debug {
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    viewBinding {
        enabled = true
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = 1.8
    }
}

dependencies {
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    wearApp project(':wear')
    implementation project(':common')
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "androidx.viewpager2:viewpager2:$viewpager2Version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"
    implementation "androidx.core:core-ktx:$ktxVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesAndroidVersion"

    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation 'com.google.android.gms:play-services-wearable:17.0.0'
    implementation "com.heinrichreimersoftware:material-intro:$materialIntroVersion"
    implementation "cc.chenhe:wear-msger:$wearMsgerVersion"

    // AndroidLinks
    implementation 'com.drakeet.about:about:2.4.1'
    implementation 'com.drakeet.multitype:multitype:4.0.0'
    implementation 'com.drakeet.about:about-extension:2.4.1'

    // pictures
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation "com.davemorrissey.labs:subsampling-scale-image-view:$subsamplingImageViewVersion"

    // firebase
    implementation 'com.google.firebase:firebase-analytics:17.4.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'

    testImplementation "junit:junit:$junitVersion"
    testImplementation "androidx.test:core:$testVersion" // Robolectric environment
    testImplementation "org.mockito:mockito-core:$mockitoVersion" // Mockito framework
    testImplementation "org.amshove.kluent:kluent-android:$kluentVersion"
}
