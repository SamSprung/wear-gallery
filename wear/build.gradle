apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
//firebase
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'


def localProperties = new Properties()
def localPropertiesFile = rootProject.file("config.properties")
if (localPropertiesFile.isFile()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
} else {
    logger.log(LogLevel.ERROR, "Can't find file <config.properties>. Please see <config-tmpl.properties>.")
    localProperties['ks.file'] = "n/a"
}

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "cc.chenhe.weargallery"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 220600011 // header(22)+xx.xx.xx+device(0-phone; 1-wear)
        versionName "v6.0.1-preview"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }
    signingConfigs {
        release {
            storeFile file(localProperties['ks.file'])
            storePassword localProperties['ks.pwd']
            keyAlias localProperties['ks.alias']
            keyPassword localProperties['ks.aliaspwd']
        }
    }
    buildTypes {
        debug {
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    viewBinding {
        enabled = true
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = 1.8
        freeCompilerArgs += ['-Xopt-in=kotlin.RequiresOptIn']
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/core-3.3.3.jar')
    implementation project(':common')
    implementation project(':wearvision')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:$ktxVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.fragment:fragment:$fragmentVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.viewpager2:viewpager2:$viewpager2Version"
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"
    implementation "androidx.annotation:annotation:$annotationVersion"
    implementation "androidx.palette:palette:$paletteVersion"
    implementation "androidx.exifinterface:exifinterface:$exifinterfaceVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    implementation "org.koin:koin-core:$koinVersion"
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"

    implementation "cc.chenhe:wear-msger:$wearMsgerVersion"
    implementation 'com.google.android.gms:play-services-wearable:17.0.0'
    implementation 'com.google.android.support:wearable:2.6.0'
    compileOnly 'com.google.android.wearable:wearable:2.6.0'
    implementation 'cc.chenhe:watch-face-helper:2.0.3'
    implementation "com.heinrichreimersoftware:material-intro:$materialIntroVersion"
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    implementation "me.panpf:sketch:$sketchVersion"
    implementation "me.panpf:sketch-gif:$sketchVersion"

    // firebase
    implementation 'com.google.firebase:firebase-analytics:17.4.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.amshove.kluent:kluent-android:$kluentVersion"
    testImplementation "org.koin:koin-test:$koinVersion"
}
